# Decoding variable precision extensions accessed through the ROCC interface
#
# Copyright (c) 2020-2020 Frédéric Pétrot 
# 
# This program is free software; you can redistribute it and/or modify it
# under the terms and conditions of the GNU General Public License,
# version 2 or later, as published by the Free Software Foundation.
# 
# This program is distributed in the hope it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for
# more details.
# 
# You should have received a copy of the GNU General Public License along with
# this program.  If not, see <http://www.gnu.org/licenses/>.
#
# https://qemu.weilnetz.de/doc/devel/decodetree.html


#############################################
# Arbitrary Precision Floating Point 64-bit #
#############################################

# *** VP load and store arbitrary precision registers
flp           ............   ..... 101 ..... 0000111 @i
fsp           ....... .....  ..... 101 ..... 0100111 @s

# *** VP arithmetic operations
fadd_p        0000011 ..... ..... ... ..... 0001011 @r_rm
fsub_p        0000111 ..... ..... ... ..... 0001011 @r_rm
fmul_p        0001011 ..... ..... ... ..... 0001011 @r_rm
fdiv_p        0001111 ..... ..... ... ..... 0001011 @r_rm
fsqrt_p       0101111 00000 ..... ... ..... 0001011 @r2_rm
feq_p         1010011 ..... ..... 010 ..... 0001011 @r
flt_p         1010011 ..... ..... 001 ..... 0001011 @r
fle_p         1010011 ..... ..... 000 ..... 0001011 @r
fmin_p        0010111 ..... ..... 000 ..... 0001011 @r
fmax_p        0010111 ..... ..... 001 ..... 0001011 @r
fsgnj_p       0010011 ..... ..... 000 ..... 0001011 @r
fsgnjn_p      0010011 ..... ..... 001 ..... 0001011 @r
fsgnjx_p      0010011 ..... ..... 010 ..... 0001011 @r
fmadd_p       ..... 00 ..... ..... ... ..... 0001011 @r4_rm
fmsub_p       ..... 01 ..... ..... ... ..... 0001011 @r4_rm

# *** VP conversions
# Conversion double into mpfr
fcvt_p_d     1100011 00001 ..... ... ..... 0001011 @r2_rm

# Conversion mpfr into double
fcvt_d_p     1100011 00010 ..... ... ..... 0001011 @r2_rm

# Conversion int into mpfr
fcvt_p_l      1100011 01001 ..... ... ..... 0001011 @r2_rm

# Conversion mpfr into int
fcvt_l_p      1100011 01010 ..... ... ..... 0001011 @r2_rm

# Conversion uint into mpfr
fcvt_p_lu     1100011 01011 ..... ... ..... 0001011 @r2_rm

# Conversion mpfr into uint
fcvt_lu_p     1100011 01100 ..... ... ..... 0001011 @r2_rm